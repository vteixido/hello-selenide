plugins {
    id 'java'
    id 'io.qameta.allure' version '2.9.3'
    id 'jacoco'
    id 'pmd'
    id 'info.solidsoft.pitest' version '1.7.0'
}

group 'com.sinensia'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    allureVersion = '2.16.1'
    junitVersion = '5.8.1'
    cucumberVersion = '7.2.3'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation('com.codeborne:selenide:5.25.0')
    testImplementation("io.qameta.allure:allure-selenide:${allureVersion}")
    testImplementation('org.hamcrest:hamcrest:2.2')
    testImplementation('org.slf4j:slf4j-simple:1.7.30')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    testImplementation("io.cucumber:cucumber-java:${cucumberVersion}")
    testImplementation("io.cucumber:cucumber-junit:${cucumberVersion}")
    testImplementation("org.junit.vintage:junit-vintage-engine:${junitVersion}")
    testImplementation("io.qameta.allure:allure-cucumber4-jvm:2.15.0")

}

tasks.withType(Test){
    systemProperties["selenide.baseUrl"]="http://10.250.3.2:3000"
    systemProperties["selenide.remote"]="http://localhost:4444/wd/hub"
    systemProperties["selenide.browser"]="chrome"
    //systemProperties["selenide.browserVersion"]="97.0"
    systemProperties["selenide.browserSide"]="1024x768"
    ignoreFailures = true
    useJUnitPlatform()
    finalizedBy jacocoTestReport


}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

task 'testFirefox' (type:Test) {
    systemProperties["selenide.browser"]="firefox"

}

task 'testFirefox95' (type:Test) {
    systemProperties["selenide.browser"]="firefox"
    systemProperties["selenide.browserVersion"]="95.0"
}

task 'testEdge' (type:Test) {
    systemProperties["selenide.browser"]="edge"

}

task 'testOpera' (type:Test) {
    systemProperties["selenide.browser"]="opera"
    systemProperties["selenide.browserSide"]="600x800"


}
allure {
    autoconfigure = true
    version = "${allureVersion}"

    useJUnit5 {
        version = "${allureVersion}"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'com.sinensia',
                    'src/test/resources',
            ]
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

pmd {
    consoleOutput = true
    ignoreFailures = true
}

pitest {
    junit5PluginVersion = '0.12'
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}